plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.cloud.tools.jib' version '3.4.2'
}

group = 'demo.venu'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	agent
}

repositories {
	mavenCentral()
}

dependencies {
	agent "io.opentelemetry.javaagent:opentelemetry-javaagent:1.32.0"
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
}
tasks.named('test') {
	useJUnitPlatform()
}
task copyAgent(type: Copy) {
	from configurations.agent {
		rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
	}
	into layout.buildDirectory.dir("agent")
}

bootJar {
	dependsOn copyAgent
	archiveFileName = "app.jar"
}

jib {
//	from {
//		image = 'mcr.microsoft.com/openjdk/jdk:17-ubuntu'
//	}
//	to{
//		image = 'venuacr.azurecr.io/employee:0.0.1-SNAPSHOT'
//		auth {
//			username = "YOUR-ACR"
//			password = "YOUR-ACR-PASSWORD"
//		}
//	}
	extraDirectories {
		paths {
			path {
				from = layout.buildDirectory.dir("agent")
				into = '/otelagent'
			}
		}
	}

	container {
		jvmFlags = [
				'-javaagent:/otelagent/opentelemetry-javaagent.jar'
		]
	}
}

tasks.named('jibDockerBuild').configure {
	dependsOn copyAgent
}
